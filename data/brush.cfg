// this config defines various heightmap brushes

// a brush has two parts: the brushmap and the brushhandle

// the brushmap is a 2D field that defines how the brush affects a heightmap

// the brushhandle is a 2D vertex on the brushmap.
// this handle is a reference point on the brushmap which represents
// where the editing cursor is pointed at.

// clearbrush                   : resets the brushmap
// brushvert <x> <y> <value>    : set a point on the brushmap
// brushx <x>                   : the x coordinate of the brushhandle
// brushy <y>                   : the y coordinate of the brushhandle

alias brushhandle [
  brushx $arg1
  brushy $arg2
]

// bv <y> <row size> <... row values ...>
alias bv [
  loop $arg2 [
    at $arg3 $i
    brushvert $i $arg1 $s
  ]
]

alias brushindex 0
alias brushmax 3

alias selectbrush [
  alias brushindex ( + $brushindex $arg1 )
  if (< $brushindex 0) [ alias brushindex $brushmax ]
  if (> $brushindex $brushmax) [ alias brushindex 0 ]

  concatword brush _ $brushindex
  s
  echo $brushname
]

alias brush_0 [
  alias brushname "No Brush"
  clearbrush
]

alias brush_1 [
  alias brushname "Circle 1-0 Brush"
  clearbrush
  brushhandle 0 0
  brushvert 0 0 1
]

alias brush_2 [
  alias brushname "Circle 2-1 Brush"
  clearbrush
  brushhandle 2 2
  bv 1 3 "0 0 1 "
  bv 2 4 "0 1 2 1 "
  bv 3 3 "0 0 1 "
]

alias brush_3 [
  alias brushname "Circle 4-2-1 Brush"
  clearbrush
  brushhandle 2 2
  bv 0 3 "0 0 1 "
  bv 1 4 "0 1 2 1 "
  bv 2 5 "1 2 4 2 1 "
  bv 3 4 "0 1 2 1 "
  bv 4 3 "0 0 1 "
]
