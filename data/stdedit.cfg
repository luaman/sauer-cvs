
// implement some editing commands

clearents = [ 
  if $editing [
    esel [ strcmp (et) $arg1 ]; 
    echo Deleted (esellen) $arg1 entities; 
    delent
  ] 
]

replaceents = [
  if $editing [
    type = $arg1
    t  = (et)
    a0 = (ea 0)
    a1 = (ea 1)
    a2 = (ea 2)
    a3 = (ea 3)
    esel [ && (= (ea 0) $a0) (&& (&& (strcmp (et) $t) (= (ea 1) $a1)) (&& (= (ea 2) $a2) (= (ea 3) $a3))) ]
    entedit $type $arg2 $arg3 $arg4 $arg5
    echo Replaced (esellen) entities  
  ]
]

selentedit  = [ "saycommand" ( concatword "/entedit " (et) " " (ea 0) " " (ea 1) " " (ea 2) " " (ea 3)) ] 

selreplaceents  = [ "saycommand" ( concatword "/replaceents " (et) " " (ea 0) " " (ea 1) " " (ea 2) " " (ea 3)) ]  

entproperty = [
  a0 = (ea 0)
  a1 = (ea 1)
  a2 = (ea 2)
  a3 = (ea 3)
  do [a@arg1 = (+ (ea @arg1) @arg2)]
  entedit (et) $a0 $a1 $a2 $a3
]

selcorners = [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0; ] ]
editdrag   = [ dragging 1; onrelease [ dragging 0 ] ]

editmove   = [ 
  selextend
  reorient
  moving 1
  onrelease [ moving 0 ] 
]

editgrab    = [  
  copy
  delcube
  onrelease [ paste ] 
]

movedrag    = [
  moving 1
  if (= $moving 1) reorient [ dragging 1 ]
  onrelease [ moving 0; dragging 0 ]
]

cancelall = [ cancelsel; ecancelsel ] 

