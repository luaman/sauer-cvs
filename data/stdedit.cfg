
// implement some editing commands

clearents = [ 
  if $editing [
    entcancel
    entselect [ strcmp (et) $arg1 ]; 
    echo Deleted (enthavesel) $arg1 entities; 
    delent
  ] 
]

replaceents = [
  if $editing [
    type = $arg1
    t  = (et)
    a0 = (ea 0)
    a1 = (ea 1)
    a2 = (ea 2)
    a3 = (ea 3)
    entcancel
    entselect [ && (= (ea 0) $a0) (&& (&& (strcmp (et) $t) (= (ea 1) $a1)) (&& (= (ea 2) $a2) (= (ea 3) $a3))) ]
    entedit $type $arg2 $arg3 $arg4 $arg5
    echo Replaced (enthavesel) entities  
  ]
]

selentedit  = [ "saycommand" ( concatword "/entedit " (et) " " (ea 0) " " (ea 1) " " (ea 2) " " (ea 3)) ] 

selreplaceents  = [ "saycommand" ( concatword "/replaceents " (et) " " (ea 0) " " (ea 1) " " (ea 2) " " (ea 3)) ]  

entfind    = [ entcancel; entselect [ strcmp (et) $arg1 ] ]

entproperty = [
  a0 = (ea 0)
  a1 = (ea 1)
  a2 = (ea 2)
  a3 = (ea 3)
  do [a@arg1 = (+ (ea @arg1) @arg2)]
  entedit (et) $a0 $a1 $a2 $a3
]

enttoggle = [ entmoving 1; entmoving 0; ]
entaddmove = [ 
  h = (enthavesel)
  entmoving 1 
  if (< (enthavesel) $h) [ 
    entmoving 1
  ]
]
entadd = [ entaddmove; entmoving 0; ]

entdrag    = [ entaddmove; onrelease [entmoving 0]; result $entmoving ] 
editdrag   = [ if (entdrag) [] [ dragging 1; onrelease [ dragging 0 ] ] ]
selcorners = [ if (entdrag) [] [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0; ] ] ]

editrotate = [ rotate $arg1; entcancel; entselect insel; entrotate $arg1 ] 
editflip   = [ flip;         entcancel; entselect insel; entflip ]

editextend   = [ 
  selextend
  reorient
  moving 1
  onrelease [ moving 0 ] 
]

editgrab    = [  
  copy
  delcube
  onrelease [ paste ] 
]

editmovewith    = [
  if (havesel) [
    moving 1
    if (= $moving 0) [ @arg1 ]
    onrelease [ moving 0; dragging 0 ]
  ] [
    @arg1
  ]
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]

cancelall = [ cancelsel; entcancel ]

editfacewentpush = [ if ($entmoving) [entpush $arg1] [editface $arg1 $arg2] ]

