
// implements some editing commands


//////// Entity Editing ///////////////

// the entity type of current selection
et = [ at (entget) 0 ]

// access the given attribute of selected ent
ea = [ at (entget) (+ $arg1 1) ]

// clear ents of given type
clearents = [ 
  if $editing [
    entcancel
    entselect [ strcmp (et) $arg1 ]; 
    echo Deleted (enthavesel) $arg1 entities; 
    delent
  ] 
]

// replace all ents that match current selection
// with the values given
replaceents = [
  if $editing [
    entfind @(entget)
    entset @arg1 @arg2 @arg3 @arg4 @arg5
    echo Replaced (enthavesel) entities  
  ]
]

selentedit      = [ "saycommand" ( concatword "/entset " (entget) ) ] 
selreplaceents  = [ "saycommand" ( concatword "/replaceents " (entget) ) ]  
selentfindall   = [ do [ @( concatword "entfind " (entget) ) ] ]

// modify given attribute of ent by a given amount
entproperty = [
  entloop [
    a0 = (ea 0)
    a1 = (ea 1)
    a2 = (ea 2)
    a3 = (ea 3)
    do [a@arg1 = (+ (ea @arg1) @arg2)]
    entset (et) $a0 $a1 $a2 $a3
  ]
]

enttypelist = [ 
  light mapmodel playerstart envmap particles sound
  shells bullets rockets riflerounds grenades cartridges
  health healthboost greenarmour yellowarmour quaddamage
  teleport teledest
  monster carrot jumppad
  base respawnpoint
]

enttypelength = (listlen $enttypelist)

listindex = [
  s = 0
  n = (listlen $arg1)
  loop i $n [
    if (strcmp $arg2 (at $arg1 $i)) [
      s = $i
    ]
  ]
  result $s
]

enttypeselect = [     
  n = $enttypelength
  next = (+ (listindex $enttypelist (et)) $arg1)
  if (< $next 0)  [ next = (- $n 1) ]
  if (= $next $n) [ next = 0 ]
  next = (at $enttypelist $next)
  entset $next (ea 0) (ea 1) (ea 2) (ea 3) 
]

//////// Copy and Paste //////////////

entcopybuf = ""

entreplace   = [ 
  if (! (strcmp "" $entcopybuf)) [ 
    if (enthavesel) [
      entset @entcopybuf 
    ] [
      newent @entcopybuf // sometimes newent will replace some args
      entset @entcopybuf // ensure exact copy
    ]
    result 1
  ] [
    result 0
  ]
]

editcopy   = [
  if (|| havesel [! (enthavesel)]) [
    entcopybuf = ""
    entcopy
    copy
  ] [
    entcopybuf = (entget)
  ]
]

editpaste  = [ 
  cancelpaste = (! (|| enthavesel havesel));
  || entreplace paste
  onrelease [ 
    if (strcmp "" $entcopybuf) entpaste
    if ($cancelpaste) [ cancelsel ] 
  ] 
]

/////// Selection ///////////////

equaltype  = [
  if (strcmp * $arg1) [ result 1 ] [
    result (strcmp (et) $arg1)
  ]
]

equalattr  = [
  if (strcmp * $arg2) [ result 1 ] [
    result (= (ea $arg1) $arg2)
  ]
]

// select ents with given properties
// '*' is wildcard
entfind    = [ 
  if (= ($numargs) 1) [
    entselect     [ equaltype @@arg1 ]     
  ] [ if (= ($numargs) 2) [
    entselect [ && (equaltype @@arg1)         [equalattr 0 @@arg2] ]
  ] [ if (= ($numargs) 3) [
    entselect [ && (equaltype @@arg1) (&&     [equalattr 0 @@arg2] [equalattr 1 @@arg3]) ]
  ] [ if (= ($numargs) 4) [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3])     [equalattr 2 @@arg4]) ]
  ] [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3]) (&& [equalattr 2 @@arg4] [equalattr 3 @@arg5])) ]
  ]]]]
]

lse        = [
  line = ""
  count = 0
  entloop [
    line  = ( concatword $line (entget) "		" )
    count = ( + $count 1 )
    if (> $count 4) [
      echo $line
      line = ""
      count = 0
    ]
  ]
  if (> $count 0 ) [ echo $line ]
  echo (enthavesel) entities selected
]

enttoggle  = [ entmoving 1; entmoving 0; ]
entaddmove = [ entmoving 2 ]

grabbing = 0

drag       = [ dragging 1; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0; ] ]
entadd     = [ entaddmove; entmoving 0; ]
editmove   = [ moving 1; onrelease [ moving 0 ]; result $moving ]
entdrag    = [ entaddmove; onrelease [entmoving 0]; result $entmoving ] 
editdrag   = [ cancelsel; || (entdrag) [ drag ] ]
selcorners = [ cancelsel; || (entdrag) [ corners ] ]

editextend   = [ 
  || entdrag [ selextend; reorient; editmove ]
]

editmovewith    = [
  if (havesel) [
    || (editmove) [ @arg1 ]
    onrelease [ moving 0; dragging 0 ]
  ] [
    @arg1
  ]
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]

////// Other Editing commands /////////

editfacewentpush = [ 
  if ($entmoving) [
    entpush $arg1
  ] [ 
    if ($moving) [ 
      pushsel $arg1 
    ] [ 
      editface $arg1 $arg2
    ] 
  ] 
]

entswithdirection = "playerstart teledest mapmodel monster"

inlist = [
  r = 0
  n = ( listlen $arg2 )
  loop i $n [
    if (strcmp $arg1 (at $arg2 $i)) [
      r = 1 
    ]
  ]
  result $r
]

entdirection = [
  if ( && (enthavesel) [ = (havesel) 0 ] ) [
    if (inlist (et) $entswithdirection) [
      if (> $arg1 0) [
        entproperty 0 @@arg2
        if (> (ea 0) 360) [ entproperty 0 -360 ]          
      ] [
        entproperty 0 -@@arg2
        if (< (ea 0) 0) [ entproperty 0 360 ]
      ]
    ]
    result 1
  ] [
    result 0
  ]
]

editflip   = [ 
  flip; 
  if ( ! $passthroughent ) [
    entcancel; 
    entselect insel; 
    entflip 
    if (! (havesel) ) [ entcancel ]
  ]
]

editrotate = [ 
  if ( $passthroughent ) [ rotate $arg1 ] [
    || [ entdirection $arg1 15 ] [ 
      rotate $arg1
      entcancel
      entselect insel
      entrotate $arg1 
      if (! (havesel) ) [ entcancel ]
    ] 
  ]
]

editcut    = [  
  hadselection = (havesel)
  moving 1
  if ($moving) [
    copy  
    delcube
    if (! $passthroughent ) [
      entselect insel
      entcopy
      delent
      T_T = 1
    ]
    onrelease [ 
      moving 0
      paste
      if ( $T_T ) entpaste
      if ( ! $hadselection ) [ cancelsel ]
    ]
  ]
]

passthrough = [ passthroughcube $arg1; passthroughsel $arg1; passthroughent $arg1 ]

editdel = [ delent; delcube ]

