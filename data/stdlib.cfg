// console language standard library

// executes the body supplied
// i.e. do [@stuff]
alias do [ arg1 ]

// concats all the results of each iteration
// i.e. loopconcat 5 [result $i]
// results in "0 1 2 3 4"
alias loopconcat [
    alias sum "";
    loop $arg1 [alias sum (concat $sum (arg2))]
    result $sum;
]

// creates a macro whose body is a format string
// i.e. macro greet [ say Hi, %1! ]
alias macro [
    alias $arg1 [do (concat [format [@@@arg2]] @[loopconcat $numargs [concatword " $arg" (+ $i 1)]])]
]

// binds a key so that it will toggle a variable
// i.e. bindvar 9 thirdperson
alias bindvar [
  bind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]

// same as above, but only binds for edit mode
alias editbindvar [
  editbind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]

