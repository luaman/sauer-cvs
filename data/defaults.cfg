// these default settings get executed whenever "config.cfg" is not available
// do not modify anything below, instead change settings in game, or add to autoexec.cfg

name "unnamed"
team "sauerbraten"

invmouse 0         // 1 for flightsim mode
sensitivity 3      // similar number to quake
fov 100            // 90 is default in other games

musicvol 40       // set higher if you want (max 255)
soundvol 255

gamma 100          // set to your liking, 100 = default


// WASD

bind W forward
bind A left
bind S backward
bind D right

bind UP "forward"
bind DOWN "backward"
bind RIGHT "right"
bind LEFT "left"

bind 0 "weapon 0 1 2"
bind 1 "weapon 1 2 4"
bind 2 "weapon 2 1 4"
bind 3 "weapon 3 4 2"
bind 4 "weapon 4 3 2"
bind 5 "weapon 5 3 1"
bind 6 "weapon 6 2 4"

bind SPACE "jump"
bind MOUSE2 "jump"
bind MOUSE1 "attack"
bind MOUSE3 "weapon"

bind R repammo

bind TAB "showscores"

bind T "saycommand"
bind BACKQUOTE "saycommand /"

bind E "edittoggle"

bind KP_MINUS "conskip 1"
bind KP_PLUS "conskip -1000"

bindvar F1 paused

bind F11 "toggleconsole"
bind F12 "screenshot"

hudgun 1

alias sp      [ mode -2; map $arg1 ]
alias dmsp    [ mode -1; map $arg1 ]
alias ffa     [ mode  0; map $arg1 ]
alias insta   [ mode  4; map $arg1 ]
alias capture [ mode 12; map $arg1 ]

alias mapcomplete [ complete $arg1 packages/base ogz ]
mapcomplete map
mapcomplete sp
mapcomplete dmsp
mapcomplete ffa
mapcomplete insta
mapcomplete capture
complete importcube packages/cube cgz


//////////////////////////////////
// universal scrollwheel + modifier commands:

alias defaultmodifier 0
alias modifier $defaultmodifier
alias domodifier [ alias modifier $arg1; onrelease [ alias modifier $defaultmodifier ] ]

alias universaldelta [
  do (concatword delta _ (if $editing [ result edit ] [ result game ]) _ $modifier)
]

bind MOUSE4 [ universaldelta 1 ]	// also used for editing, see below
bind MOUSE5 [ universaldelta -1 ]

//////////////////////////////////

alias delta_game_0 [ if (= $arg1 1) [ weapon 2 3 5 ] [ weapon 4 1 5 ] ]

alias zfov 120
alias delta_game_1 [
  alias zfov (+ $zfov (* $arg1 (div $zfov -5)))
  if (< $zfov 10)  [ alias zfov 10 ]
  if (> $zfov 120) [ alias zfov 120 ]
  fov $zfov
]

bind G [ domodifier 1 ]


//////////////////////////////////
// Sauerbraten Editing related bindings
// found in autoexec.cfg in your sauerbraten directory



editbind LSHIFT reorient
editbind LCTRL passthrough

editbind X flip
editbind C copy
editbind V paste
editbind Z undo
editbind U undo
editbind I redo

alias brushcopy 0
editbind H [ getheightmap; if (= $brushcopy 0) [ selectbrush 0 ]; ]
editbind K [ copybrush; alias brushcopy 1; ]

editbind BACKSPACE delent

editbind B selextend

editbind MOUSE3 selcorners

// commands below are used with scroll wheel (and universaldelta).
// if you have no scroll wheel or don't want to use it,
// you can either change the binds of universaldelta
// ex:  bind MOUSE4 [ universaldelta 1 ]
//      bind MOUSE5 [ universaldelta -1 ]
// can be changed to:
//      bind INSERT [ universaldelta 1 ]
//      bind DELETE [ universaldelta -1 ]
// so that the INSERT and DELETE key act just like a scroll wheel
//
// or you can rebind all the commands to seperate keys
// ex:  bind T [ domodifier 6 ]
// can be changed to:
//      bind HOME [ edittex 1 ]
//      bind END [ edittex -1 ]


editbind G [ domodifier 1 ] // domodifier 1 -> executes delta_edit_1
editbind F [ domodifier 2 ] // etc...
editbind Q [ domodifier 3 ]
editbind R [ domodifier 4 ] 
editbind Y [ domodifier 6 ]
editbind J [ domodifier 9; alias brushcopy 0; ]


alias delta_edit_0 [ editface $arg1 1 ] // Fill and Empty cube
alias delta_edit_1 [ gridpower (+ $arg1 $gridpower) ]
alias delta_edit_2 [ editface $arg1 0 ] // push face/corners selected
alias delta_edit_3 [ editface $arg1 2 ] // push corner pointed at by cursor
alias delta_edit_4 [ rotate $arg1 ] // rotate 90 degrees
alias delta_edit_5 [ entproperty 0 $arg1 ]  // and the others
alias delta_edit_6 [ edittex $arg1 ] // change textures
alias delta_edit_9 [ selectbrush $arg1 ] // change heightmap brushes


alias air [ editmat air ]
alias water [ editmat water ]
alias clip [ editmat clip ]
alias glass [ editmat glass ]
alias noclip [ editmat noclip ]

editbindvar 8 wireframe
bindvar 9 thirdperson
editbindvar 0 allfaces
editbindvar L fullbright

