// standard shader definitions

alias vpstart [
!!ARBvp1.0
ATTRIB opos = vertex.position; 
OUTPUT spos = result.position; 
DP4 spos.x, state.matrix.mvp.row[0], opos; 
DP4 spos.y, state.matrix.mvp.row[1], opos; 
DP4 spos.z, state.matrix.mvp.row[2], opos; 
DP4 spos.w, state.matrix.mvp.row[3], opos;
]

///////////////////////////////////////////////////
//
// used for any textured polys that don't have a shader set
//
///////////////////////////////////////////////////

shader "default" [		
@vpstart
MOV result.texcoord[0], vertex.texcoord[0];
MOV result.color, vertex.color;
END
] [
!!ARBfp1.0
TEMP diffuse;
TEX diffuse, fragment.texcoord[0], texture[0], 2D;
MUL result.color, fragment.color, diffuse;
END
]

//////////////////////////////////////////////////////////////////////
//
// same, but now without texture sampling (some HUD stuff needs this)
//
//////////////////////////////////////////////////////////////////////

shader "notexture" [		
@vpstart
MOV result.color, vertex.color;
END
] [
!!ARBfp1.0
MOV result.color, fragment.color;
END
]

////////////////////////////////////////////////////////
//
// default lightmapped world shader.. does texcoord gen
//
///////////////////////////////////////////////////////

shader "stdworld" [
@vpstart
DP3 result.texcoord[0].x, opos, state.texgen[0].object.s;
DP3 result.texcoord[0].y, opos, state.texgen[0].object.t;
MOV result.texcoord[1], vertex.texcoord[1];
DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
END
] [
!!ARBfp1.0
OPTION ARB_fog_linear;
TEMP diffuse, lm;
TEX lm,      fragment.texcoord[0], texture[0], 2D;
TEX diffuse, fragment.texcoord[1], texture[1], 2D;
MUL lm, lm, 2;
MUL result.color, lm, diffuse;
END
]

////////////////////////////////////////////////
//
// phong lighting model shader
//
////////////////////////////////////////////////

shader "stdmodel" [
@vpstart
ATTRIB onormal = vertex.normal; 
PARAM ocampos = program.env[1];
PARAM lightdir = program.env[0];
OUTPUT wnormal = result.texcoord[1]; 
TEMP halfangle;
MOV result.texcoord[0], vertex.texcoord[0];
MOV wnormal, onormal;
MOV result.color, vertex.color;
SUB halfangle, ocampos, opos;
DP3 halfangle.w, halfangle, halfangle;
RSQ halfangle.w, halfangle.w;
MUL halfangle.xyz, halfangle.w, halfangle;
ADD result.texcoord[3], lightdir, halfangle;
MOV result.texcoord[2], lightdir;
DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
END
] [
!!ARBfp1.0
OPTION ARB_fog_linear;
ATTRIB normal = fragment.texcoord[1];
ATTRIB lightdir = fragment.texcoord[2];
ATTRIB halfangle = fragment.texcoord[3];

PARAM specintensity = program.env[2];
PARAM ambient = program.env[3];

TEMP diffuse, rnormal, rhalfangle, light, spec, x;

DP3 rhalfangle.w, halfangle, halfangle;
RSQ rhalfangle.w, rhalfangle.w;
MUL rhalfangle.xyz, rhalfangle.w, halfangle;

DP3 rnormal.w, normal, normal;
RSQ rnormal.w, rnormal.w;
MUL rnormal.xyz, rnormal.w, normal;

DP3 spec, rhalfangle, rnormal;
MAX spec, spec, 0;
MOV x, 128;
POW spec, spec.x, x.x;
MUL spec, spec, specintensity;

DP3 light, rnormal, lightdir;
MUL light, light, 1.5;
MAX light, light, ambient;
#MOV light.w, 1;

TEX diffuse, fragment.texcoord[0], texture[0], 2D;
MAD light, light, diffuse, spec;
MUL result.color, light, fragment.color;
END
]
