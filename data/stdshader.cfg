// standard shader definitions

fpopts = [
    @(if (= $shaderprecision 1) [result "OPTION ARB_precision_hint_fastest;"])
    @(if (= $shaderprecision 3) [result "OPTION ARB_precision_hint_nicest;"])
]

vpstart = [
	!!ARBvp1.0
	ATTRIB opos = vertex.position; 
	OUTPUT spos = result.position; 
	DP4 spos.x, state.matrix.mvp.row[0], opos; 
	DP4 spos.y, state.matrix.mvp.row[1], opos; 
	DP4 spos.z, state.matrix.mvp.row[2], opos; 
	DP4 spos.w, state.matrix.mvp.row[3], opos;
]

fpstart = [
    !!ARBfp1.0
    @fpopts
]

macro normalize [
	DP3 %1.w, %2, %2;
	RSQ %1.w, %1.w;
	MUL %1.xyz, %1.w, %2;
]

///////////////////////////////////////////////////
//
// used for any textured polys that don't have a shader set
//
///////////////////////////////////////////////////

shader 0 "default" [		
	@vpstart
	MOV result.texcoord[0], vertex.texcoord[0];
	MOV result.color, vertex.color;
	END
] [
    @fpstart
	TEMP diffuse;
	TEX diffuse, fragment.texcoord[0], texture[0], 2D;
	MUL result.color, fragment.color, diffuse;
	END
]

//////////////////////////////////////////////////////////////////////
//
// same, but now without texture sampling (some HUD stuff needs this)
//
//////////////////////////////////////////////////////////////////////

shader 0 "notexture" [		
	@vpstart
	MOV result.color, vertex.color;
	END
] [
    @fpstart
	MOV result.color, fragment.color;
	END
]

////////////////////////////////////////////////////////
//
// default lightmapped world shader.. does texcoord gen
//
///////////////////////////////////////////////////////


worldshader = [
	shader 0 $arg1 [
		@vpstart
		DP3 result.texcoord[0].x, opos, program.env[0];
		DP3 result.texcoord[0].y, opos, program.env[1];
		DP3 result.texcoord[1].x, state.matrix.texture[1].row[0], vertex.texcoord[1];
		DP3 result.texcoord[1].y, state.matrix.texture[1].row[1], vertex.texcoord[1];
		DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
		MOV result.color, vertex.color;
		END
	] [
        @fpstart
		OPTION ARB_fog_linear;
		TEMP diffuse, lm;
		TEX diffuse, fragment.texcoord[0], texture[0], 2D;
		TEX lm,      fragment.texcoord[1], texture[1], 2D;
		@arg2
		MUL diffuse, diffuse, 2;
		MUL result.color, diffuse, lm;
		END
	]
]

worldshader "stdworld" ""
worldshader "decalworld" [
	TEMP decal;
	TEX decal,   fragment.texcoord[0], texture[2], 2D;
	LRP diffuse, decal.w, decal, diffuse;
]

bumpshader = [
	parallax = $arg2		// overridden by normalize, ugh
    shader 1 $arg1 [
	    @vpstart
	    DP3 result.texcoord[0].x, opos, program.env[0];
	    DP3 result.texcoord[0].y, opos, program.env[1];
	    DP3 result.texcoord[1].x, state.matrix.texture[1].row[0], vertex.texcoord[1];
	    DP3 result.texcoord[1].y, state.matrix.texture[1].row[1], vertex.texcoord[1];
	    DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
    	
	    TEMP camv, camvts, normal, tangent, binormal, x, y;
	    MAD normal, vertex.color, 2, -1;
    	
	    # TS gram-schmidt orthonormalization, needed for non-90 degree surfaces (expensive, TS only used for eye vector! can remove if no spec/parallax)
	    DP3 x, normal, program.env[2];
	    MUL x, x, normal;
	    SUB tangent, program.env[2], x;
	    DP3 x, tangent, program.env[3];
	    MUL x, x, tangent;
        DP3 y, normal, program.env[3];
        MUL y, y, normal;
        SUB binormal, program.env[3], x;
        SUB binormal, binormal, y;
    	
	    # trans eye vector into TS
	    SUB camv, program.env[4], opos;
	    DP3 result.texcoord[7].x, camv, tangent;
	    DP3 result.texcoord[7].y, camv, binormal;
	    DP3 result.texcoord[7].z, camv, normal;
	    END
    ] [
        @fpstart
	    OPTION ARB_fog_linear;
	    TEMP diffuse, lmc, lmlv, bump, camvts, he, height, dtc, light;

	    TEX lmc,  fragment.texcoord[1], texture[1], 2D;
	    TEX lmlv, fragment.texcoord[1], texture[2], 2D;
	    MAD lmlv, lmlv, 2, -1;	
    	
	    @(normalize camvts fragment.texcoord[7])
	    ADD he, camvts, lmlv;
	    @(normalize he he) 

        @parallax

	    TEX diffuse, dtc, texture[0], 2D;
	    TEX bump,    dtc, texture[3], 2D;
	    MAD bump.xyz, bump, 2, -1;
    	
	    DP3_SAT he, he, bump;
	    MOV he.w, 32;
	    POW he, he.x, he.w;
	    
	    @arg3
    	
	    DP3_SAT bump, bump, lmlv;
        MUL light, lmc, bump;
        MAX light, light, program.env[5];

        MAD diffuse, diffuse, 2, he;

	    @arg4
	    
	    END
    ]
]

bumpshader "bumpspecworld"         [ MOV dtc, fragment.texcoord[0]; ] []                                                   [ MUL result.color, light, diffuse; ]

setpixelparam 1 8 8 8       // spec map multiplier
bumpshader "bumpspecmapworld"      [ MOV dtc, fragment.texcoord[0]; ] [ MUL he, he, program.env[11]; MUL he, he, bump.w; ] [ MUL result.color, light, diffuse; ]

setpixelparam 0 1 1 1       // glow color
setpixelparam 1 8 8 8       // spec map multiplier
bumpshader "bumpspecmapglowworld"  [ MOV dtc, fragment.texcoord[0]; ] [ MUL he, he, program.env[11]; MUL he, he, bump.w; ] [
    TEMP glow;
	TEX glow, dtc, texture[4], 2D;
    MUL glow, glow, program.env[10];
    MAD result.color, light, diffuse, glow;
]

bumpshader "bumpspecparallaxworld" [
    TEX height, fragment.texcoord[0], texture[3], 2D;
    MAD height.w, height.w, 0.02, -0.01;	# 0.04, -0.02;	    # current textures don't allow for much parallax scaling before it distorts
    MAD dtc, height.w, camvts, fragment.texcoord[0];
] [] [ MUL result.color, light, diffuse; ]



////////////////////////////////////////////////
//
// phong lighting model shader
//
////////////////////////////////////////////////

modelshader = [
	col = $arg2		// overridden by normalize, ugh
	shader 0 $arg1 [
		@vpstart
		ATTRIB onormal = vertex.normal; 
		PARAM ocampos = program.env[1];
		PARAM lightdir = program.env[0];
		OUTPUT wnormal = result.texcoord[1]; 
		TEMP halfangle;
		MOV result.texcoord[0], vertex.texcoord[0];
		MOV wnormal, onormal;
		MOV result.color, vertex.color;
		SUB halfangle, ocampos, opos;
		@(normalize halfangle halfangle)
		ADD result.texcoord[3], lightdir, halfangle;
		MOV result.texcoord[2], lightdir;
		DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
		END
	] [
        @fpstart
		OPTION ARB_fog_linear;
		ATTRIB normal = fragment.texcoord[1];
		ATTRIB lightdir = fragment.texcoord[2];
		ATTRIB halfangle = fragment.texcoord[3];
		PARAM specintensity = program.env[2];
		PARAM ambient = program.env[3];
		TEMP diffuse, rnormal, rhalfangle, light, spec, x;

		@(normalize rhalfangle halfangle)
		@(normalize rnormal normal)

		DP3_SAT spec, rhalfangle, rnormal;
		MOV x, 128;
		POW spec, spec.x, x.x;
		MUL spec, spec, specintensity;

		DP3_SAT light, rnormal, lightdir;

		@arg3
		
		MUL light, light, 1.5;
		MAX light, light, ambient;

		TEX diffuse, fragment.texcoord[0], texture[0], 2D;
		MAD light, light, diffuse, spec;
		MUL result.color, light, @col; 
		END
	]
]

modelshader "stdppmodel" "fragment.color" ""
modelshader "masksppmodel" "color" [			# specmap & glowmap version
	TEMP masks, color;
	TEX masks, fragment.texcoord[0], texture[1], 2D;
	ADD_SAT color, fragment.color, masks.yyyy;	# glowmap in green channel FIXME only do this once
	LRP light, masks.yyyy, 2, light;			# glowmap in green channel
	MUL spec, spec, masks.x;					# specmap in red channel
] 

////////////////////////////////////////////////
//
// gourad lighting model shader: cheaper, non-specular version for vegetation etc. gets used when spec==0
//
////////////////////////////////////////////////

shader 0 "nospecpvmodel" [
	@vpstart
	ATTRIB onormal = vertex.normal; 
	PARAM ocampos = program.env[1];
	PARAM lightdir = program.env[0];
	PARAM ambient = program.env[3];
	TEMP light;

	MOV result.texcoord[0], vertex.texcoord[0];

	DP3 light, onormal, lightdir;
	MUL light, light, 1.5;
	MAX light, light, ambient;
	MUL result.texcoord[1], light, vertex.color;

	DP4 result.fogcoord, -opos, state.matrix.modelview.row[2];
	END
] [
    @fpstart
	OPTION ARB_fog_linear;
	ATTRIB light = fragment.texcoord[1];
	TEMP diffuse;

	TEX diffuse, fragment.texcoord[0], texture[0], 2D;
	MUL result.color, light, diffuse;
	END
]



////////////////////////////////////////////////
//
// full screen shaders: 
//
////////////////////////////////////////////////


fsvs = [
	!!ARBvp1.0
	MOV result.position, vertex.position;   # woohoo, no mvp :) 
	MOV result.texcoord[0], vertex.texcoord[0];
]

fsps = [
    @fpstart
	TEMP sample;
	TEX sample, fragment.texcoord[0], texture[0], RECT;
]

setup4corners = [
	ADD result.texcoord[1], vertex.texcoord[0], { -1, -1, 0, 0 };		
	ADD result.texcoord[2], vertex.texcoord[0], {  1, -1, 0, 0 };		
	ADD result.texcoord[3], vertex.texcoord[0], { -1,  1, 0, 0 };		
	ADD result.texcoord[4], vertex.texcoord[0], {  1,  1, 0, 0 };		
]

sample4corners = [
	TEMP s00, s02, s20, s22;
	TEX s00, fragment.texcoord[1], texture[0], RECT;
	TEX s02, fragment.texcoord[2], texture[0], RECT;
	TEX s20, fragment.texcoord[3], texture[0], RECT;
	TEX s22, fragment.texcoord[4], texture[0], RECT;
]

// some simple ones that just do an effect on the RGB value...

shader 0 "invert" [ @fsvs END ] [ @fsps SUB result.color, 1, sample;   END ]
shader 0 "gbr"    [ @fsvs END ] [ @fsps MOV result.color, sample.yzxw; END ]

// sobel

shader 0 "sobel"  [ @fsvs @setup4corners END ] [
	@fsps
	@sample4corners

	TEMP t, u;

	ADD t, s00, s20;
	SUB t, t, s02;
	SUB t, t, s22;
	MUL t, t, t;

	ADD u, s00, s02;
	SUB u, u, s20;
	SUB u, u, s22;
	MUL u, u, u;

	ADD t, t, u;

	ADD result.color, sample, t;
	END
]


// bloom-ish

shader 0 "bloom_scale"   [ @fsvs @setup4corners END ] [
	@fsps
	@sample4corners

	TEMP t;
	ADD t, s02, s00;
	ADD t, t, s22;
	ADD t, t, s20;
	ADD t, t, sample;
	MUL result.color, t, 0.2; 
	END
]

shader 0 "bloom"   [ 
    @fsvs
    TEMP tc;
    MOV tc, vertex.texcoord[0];
    @(loopconcat i 6 [concat "MUL tc, tc, 0.5; MOV result.texcoord[" (+ $i 1) "], tc;"])
    END
] [
	@fsps
	TEMP scaled, t;
    @(loopconcat i 6 [
        format [ 
            TEX scaled, fragment.texcoord[%1], texture[%1], RECT;
            DP3 t, scaled, { 0.3, 0.5, 0.2, 1 };    # luminosity filter
            MUL t, t, t;
            MUL t, t, scaled;
            MAD sample, t, program.env[0].x, sample;
        ] (+ $i 1)
      ]
    )
	MOV result.color, sample;
	END
]


