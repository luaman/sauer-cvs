// rpg specific cfg stuff goes here

// for the rpg, almost all gameplay is defined _per map_ as opposed to globally for the game
// but for now a lot of stuff goes here for testing


// these are the log curve settings for computing efficiency based on points. do not modify if you don't know what these do.

r_def_logscale_x = 10
r_def_logscale_y = 25

r_def_melee       $r_def_logscale_x $r_def_logscale_y
r_def_ranged      $r_def_logscale_x $r_def_logscale_y
r_def_magic       $r_def_logscale_x $r_def_logscale_y
r_def_regen       $r_def_logscale_x $r_def_logscale_y
r_def_attackspeed $r_def_logscale_x $r_def_logscale_y
r_def_movespeed   $r_def_logscale_x $r_def_logscale_y
r_def_jumpheight  $r_def_logscale_x $r_def_logscale_y
r_def_maxhp       $r_def_logscale_x $r_def_logscale_y
r_def_tradeskill  $r_def_logscale_x $r_def_logscale_y
r_def_feared      $r_def_logscale_x $r_def_logscale_y
r_def_stealth     $r_def_logscale_x $r_def_logscale_y
r_def_hostility   $r_def_logscale_x $r_def_logscale_y
r_def_stata       $r_def_logscale_x $r_def_logscale_y
r_def_statb       $r_def_logscale_x $r_def_logscale_y
r_def_statc       $r_def_logscale_x $r_def_logscale_y

//showcharacterboundingbox 1


// some useful functions...

newrpgspawn = [ newent spawn; spawnname $arg1; ]

r_inventory = [ r_spawn $arg1; r_contain 1; r_pop ]
r_loot      = [ r_spawn $arg1; r_contain 2; r_pop ]
r_fortrade  = [ r_spawn $arg1; r_contain 4; r_pop ]



// all the objects in the game:

spawn_player = [
    r_inventory apple
    r_inventory power_gem
    r_gold 10               // give us some starting money, woo
]

spawn_apple    = [ r_worth 20 ]
spawn_wolfskin = [ r_worth 15 ]
spawn_wolfmeat = [ r_worth  4 ]

spawn_nice_armour = [ r_worth 1000; r_regen 50 ]
spawn_power_gem   = [ r_worth  100; r_melee 100 ]

spawn_mman = [
    r_model "rpg/characters/mman"
    r_ai
    
    r_gold 1000               // rich bastard
    
    r_inventory nice_armour   // he's going to be hard to kill

    r_fortrade apple          // ooh the guy carries an apple. Maybe we can trade?
    r_fortrade apple
    
    r_action "hi" [ r_say "hey, how are you!" ]
    r_action "I'm hungry" [ r_say "kill a wolf for me, and I will give you an apple" ]
    r_action "I killed a wolf" [
        r_take wolfskin [
            r_say "thanks for the wolf's skin, here's your apple"
            r_give apple
        ] [
            r_say "go kill a wolf first!"
        ]
    ]
]


spawn_dragon = [
    r_model "rpg/characters/dragon"
    r_ai
]

spawn_wolf = [
    r_model "rpg/characters/wolf"
    r_ai
    
    r_loot wolfskin
    r_loot wolfmeat
]

spawn_rat = [
    r_model "rpg/characters/rat"
    r_ai
]

spawn_npcman = [
    r_model "rpg/characters/npcman"
    r_ai
]

spawn_horse = [
    r_model "rpg/characters/horse"
    r_ai
]

spawn_grizzly = [
    r_model "rpg/characters/grizzly"
    r_ai
]

spawn_golem = [
    r_model "rpg/characters/golem"
    r_ai
]




// temp: override main menu for rpg

bind I showinventory

newgui main [
    guibutton "inventory (I)"    "showinventory"        "chest"
    guibar
    guibutton "map k_rpg1"
    guibutton "map demomap"
    guibutton "map rpg_01"
    guibutton "map rpg"
    guibutton "map rpgtest"
    guibar
    guibutton "load fps map.."   "showgui maps"
    guibutton "editing.."        "showgui editing"
    guibutton "options.."        "showgui options"
    guibutton "about.."          "showgui about"
    guibar
    guibutton "quit"             "quit"                 "exit"
]

