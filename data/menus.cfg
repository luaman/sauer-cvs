// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

newgui main [
    guibutton "load map.."       "showgui maps"
    guibutton "singleplayer.."   "showgui singleplayer"
    guibutton "multiplayer.."    "showgui multiplayer"
    guibutton "editing.."        "showgui editing"
    guibutton "options.."        "showgui options"
    guibutton "about.."          "showgui about"
    guibutton "quit"             "quit"                 "exit"
]

newgui about [
    guitext "Sauerbraten engine"
    guitext "by Wouter van Oortmerssen" 
    guitext "with contributions from Eihrul, Gilt and others"
    guitext "(for a full list of contributors see the readme)"
    guitext "http://sauerbraten.org/" 
]

maprotation = [
    n = (listlen $arg1)
    loop i $n [
        curmap = (at $arg1 $i)
        next = (mod (+ $i 1) $n)
        nextmap = (at $arg1 $next)
        (if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
    ]
]
    
genmapitems = [
    n = (listlen $arg1)
    loop i $n [
        curmap = (at $arg1 $i)
        aliasname = (concatword bestscore_ $curmap)
        title = (if (= (getalias $aliasname) 0) [ result $curmap ] [ concatword $curmap " (best score: " (getalias $aliasname) ")" ])
        guibutton $title (concat map $curmap) "sauer"
    ]
]

mainmaps = "metl4 deathtek fanatic_quake aard3c metl2 ruby curvedm metl3 nmp8_d complex douze killfactory lostinspace oasis aqueducts corruption thor"
moremaps1 = "hog2 academy tartech orbe hades paradigm wake5 roughinery shadowed DM_BS1 shindou stemple moonlite darkdeath thetowers kmap5 ot"
moremaps2 = "ogrosupply fragplaza frostbyte fanatic_castle_trap nmp10 island neonpanic orion katrez_d serpentine aard3b ksauer1 fanatic_complexities q1dm4 pgdm oddworld fanatic_caves"
oldmaps1 = "phosgene battleofthenile mechanic chronos chthon wdcd yesterday neon drianmp4 aard3 boo2 id nmp7 vootdm1 vootdm2 vootdm3 space"
oldmaps2 = "nod ogromines skycastle ladder spiralz secondevermap firstevermap"
capturemaps1 = "river_c paradigm fb_capture urban_c serenity nevil_c lostinspace face-capture nmp9 c_valley nmp4 nmp8_c fc3 ph-capture monastery corruption hades"
capturemaps2 = "asteroids venice relic frostbyte ogrosupply akroseum duomo"
ctfmaps = "duomo face-capture akroseum urban_c frostbyte wdcd"
rpgmaps = "k_rpg1 demomap rpg_01 village rpg"

maprotation (concat $mainmaps $moremaps1 $moremaps2)
maprotation (concat $oldmaps1 $oldmaps2)
maprotation (concat $capturemaps1 $capturemaps2) capture_
maprotation $ctfmaps ctf_
maprotation $rpgmaps

alias showmapshot [ guibar; guiimage (concatword "packages/base/" (at $guirollovername 0) ".jpg") $guirolloveraction 4 1 "data/cube.png"]

newgui maps [
    guilist [
      guilist [ genmapitems $mainmaps ]
      showmapshot
    ]
    guitab more
    guilist [
      guilist [ genmapitems $moremaps1 ]
      guilist [ genmapitems $moremaps2 ]
      showmapshot
    ]
    guitab old
    guilist [
      guilist [ genmapitems $oldmaps1 ]
      guilist [ genmapitems $oldmaps2 ]
      showmapshot
    ]
    guitab capture
    guilist [
      guilist [ genmapitems $capturemaps1 ]
      guilist [ genmapitems $capturemaps2 ]
      showmapshot
    ]
    guitab ctf
    guilist [
        guilist [ genmapitems $ctfmaps ]
        showmapshot
    ]
    guitab rpg_test
    guilist [
      guilist [ genmapitems $rpgmaps ]
      showmapshot
    ]
]

newgui capturemaps [
    guilist [
      guilist [ genmapitems $capturemaps1 ]
      guilist [ genmapitems $capturemaps2 ]
      showmapshot
    ]
]

newgui ctfmaps [
    guilist [
        guilist [ genmapitems $ctfmaps ]
        showmapshot
    ]
]

spmaps = "level9 vr ogrowaystation frozenbase orgosville theancientstone sp3 sp2 stsp1 n_sp1 n_sp2 door_demo box_demo platform_demo"

maprotation $spmaps

newgui spmaps [
    guilist [
        guilist [ genmapitems $spmaps ]
        showmapshot
    ]
]

newgui singleplayer [
    guibutton "Start An Army Of One"               "mode -2; showgui armyofone"
    guibutton "Start Four Bases"                   "mode -2; showgui fourbases"
    guibutton "start SP map.."                     "mode -2; showgui spmaps"
    guibutton "start DMSP map.."                   "mode -1; showgui maps"
    guibutton "start slowmotion SP map.."          "mode -5; showgui spmaps"
    guibutton "start slowmotion DMSP map.."        "mode -4; showgui maps"
    guitext   "skill (default: 3)"
    guislider skill
]

newgui armyofone [
    guilist [
        guilist [
            guibutton "mpsp6a" "sp mpsp6a" "sauer"
            guibutton "mpsp6b" "sp mpsp6b" "sauer"
            guibutton "mpsp6c" "sp mpsp6c" "sauer"
        ]
        showmapshot
    ]
]

newgui fourbases [
    guilist [
        guilist [
            guitext "Ithaqua's Depot"
            guibutton "4b1" "sp 4b1" "sauer"
            guitext "Defense Quarters"
            guibutton "4b2" "sp 4b2" "sauer" 
            guitext "Distraction"
            guibutton "4b3" "sp 4b3" "sauer" 
            guitext "Escher Space"
            guibutton "4b4" "sp 4b4" "sauer" 
        ]
        showmapshot
    ]
]

newgui multiplayer [
    guilist [
        guitext "name: " player 
        guifield newname 15 [name $newname] [newname = (getname)]
    ]
    guibutton "server browser.."  "showgui servers"
    guibutton "vote game mode / map.." "showgui gamemode"
    guibutton "lanconnect"
    guibutton "connect localhost"
    guibutton "disconnect"
    guibutton "team good"
    guibutton "team evil"
]

newgui servers [
    guistayopen [
        guibutton "update server list from master server" "updatefrommaster"
    ]
    guibar
    guiservers
]

newgui gamemode [
    guibutton "ffa/default"        "mode 0;  showgui maps"
    guibutton "coop edit"          "mode 1;  showgui maps"
    guibutton "ffa/duel"           "mode 2;  showgui maps"
    guibutton "teamplay"           "mode 3;  showgui maps"
    guibutton "instagib"           "mode 4;  showgui maps"
    guibutton "instagib team"      "mode 5;  showgui maps"
    guibutton "efficiency"         "mode 6;  showgui maps"
    guibutton "efficiency team"    "mode 7;  showgui maps"
    guibutton "insta arena"        "mode 8;  showgui maps"
    guibutton "insta clan arena"   "mode 9;  showgui maps"
    guibutton "tactics arena"      "mode 10; showgui maps"
    guibutton "tactics clan arena" "mode 11; showgui maps"
    guibutton "capture"            "mode 12; showgui capturemaps"
    guibutton "insta capture"      "mode 13; showgui capturemaps"
    guibutton "regen capture"      "mode 14; showgui capturemaps"
    guibutton "assassin"           "mode 15; showgui maps"
    guibutton "insta assassin"     "mode 16; showgui maps"
    guibutton "ctf"                "mode 17; showgui ctfmaps"
    guibutton "insta ctf"          "mode 18; showgui ctfmaps"
]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "toggle edit mode	(key E)"              "edittoggle"
    guibutton "toggle heightmap	(key H)"              ""    
    guibutton "deselect		(key SPACE)"                "cancelsel"
    guibutton "passthrough	(hold ALT)"               "passthrough"
    guibutton "select		drag left mouse   "	          ""
    guibutton "select corners	drag middle mouse "     ""
    guibutton "extend selection	right mouse"          ""
    guibutton "reorient		right mouse"                ""
    guibutton "move selection	drag right mouse"       ""
    guibutton "copy		(key C)"			                  "copy"
    guibutton "paste		(key V)"			                "paste"
    
    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"
    
        guibutton "newent playerstart   "
        guibutton "newent shells"
        guibutton "newent bullets"
        guibutton "newent rockets"
        guibutton "newent riflerounds"
        guibutton "newent grenades"    
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
        guibutton "monster.."                    "showgui monsters"
        guibutton "trigger.."                    "showgui triggers"

        guibutton "newent cartridges"    
        guibutton "newent health"
        guibutton "newent healthboost"
        guibutton "newent greenarmour"
        guibutton "newent yellowarmour"
        guibutton "newent quaddamage"
      ]
    ]

    guitab keys    
    guibutton "flip / mirror cube	(key X)"            "flip"
    guibutton "undo		(key Z, U)"                     "undo"
    guibutton "redo		(key I)"                        "redo"
    guibutton "delete		(BACKSPACE)"                  "editdel"
    guibutton "texture palette     (F2)" "showtexgui"
    guibutton "edit entity gui	(F3)" "showentgui"
    guibutton "select all in box	(N)" "entselect insel"
    guibutton "select all matching	(M)" "selentfindall"
    guibutton "center view on ent	(COMMA + scroll)" "entautoview"
    guibutton "edit entity console	(PERIOD)" "selentedit"    
    guibutton "toggle heightmap tex	(MMB in heightmapmode)" ""    
    guicheckbox "toggle outline		(key 7)"        outline
    guicheckbox "toggle wireframe		(key 8)"    wireframe


    guitab scroll
    guitext "push/pull selection		scroll wheel only"
    guitext "push the face of a cube	hold F + scroll wheel"
    guitext "change gridsize		hold G + scroll wheel"
    guitext "change textures		hold Y + scroll wheel"
    guitext "rotate         		hold R + scroll wheel"
    guitext "push corner with cursor	hold Q + scroll wheel"
    guitext "push/pull heightmap		hold LCTRL + scroll wheel"
    guitext "change heightmap brush	hold B + scroll wheel"

    guitab light
    guibutton "recompute lights (SLOW: 8xAA, alpha-masked model shadows)" "calclight 3"
    guibutton "recompute lights (SLOW: 8xAA, model shadows)" "calclight 2"
    guibutton "recompute lights (slow: 8xAA)"                "calclight 1"
    guibutton "recompute lights (default: 4xAA)"             "calclight 0"
    guibutton "recompute lights (quick: 2xAA)"               "calclight -1"
    guibutton "recompute lights (FAST: no AA, no shadows)"   "calclight -2"
    guibutton "patch lighting"                         "patchlight"

    guicheckbox "fullbright" fullbright

    guitext "lightprecision (default: 32)"
    guislider lightprecision

    guitext "lighterror (default: 8)"
    guislider lighterror

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab mapop
    guitext   Savemap:
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"    
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/mapmsg ]" 
]

newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "aiclip"                     "editmat aiclip"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui triggers [
    guibutton [invisible]				[BTmm = -1; showgui trigger2]
    guibutton [a carrot]				[BTmm = 23; showgui trigger2]
    guibutton [switch #1]				[BTmm = 24; showgui trigger2]
    guibutton [switch #2]				[BTmm = 25; showgui trigger2]
    guibutton [door #1]					[BTmm = 26; showgui trigger2]
    guibutton [door #2]					[BTmm = 27; showgui trigger2]
    guibutton [door #3]					[BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [animation loop]						[BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]				[BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]		[BTtt = 2; showgui trigger3]
    guibutton [trigger once]							[BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]				[BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]			[BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)]	[BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]						[BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]			[BTtt = 8; showgui trigger3]
    guibutton [door (open once)]						[BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]					[BTtt = 10; showgui trigger3]
    guibutton [door (locked)]						[BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]					[BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]			[BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]					[BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
	    tjt = (concat tag (concatword # $i))
	    tjc = (concat BTti "=" $i ";" "build_trigger")
	    guibutton $tjt $tjc 
    ]
]

resetlight = [
    lightcolour = 0
    lightrgb = "255 255 255"
    lightbright = 1
    lightradius = 128
]

lightcmd = [
    lightr = (at $lightrgb 0)
    lightg = (at $lightrgb 1)
    lightb = (at $lightrgb 2)
    if (= $lightbright 0) [
        if (strcmp $lightrgb "255 255 255") [
            lightr = 192; lightg = 192; lightb = 192
        ] [
            lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
         ]
    ]
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 200 100"
    guibutton (lightcmd)
    guibar
    guicheckbox "bright"    lightbright
    guitext "color:"
    guiradio "white"        lightcolour 0 [lightrgb = "255 255 255"]
    guiradio "blue"         lightcolour 1 [lightrgb = "192 192 255"]
    guiradio "red"          lightcolour 2 [lightrgb = "255 192 192"]
    guiradio "green"        lightcolour 3 [lightrgb = "192 255 192"]
    guiradio "yellow"       lightcolour 4 [lightrgb = "255 255 192"]
    guiradio "purple"       lightcolour 5 [lightrgb = "255 192 255"]
    guiradio "turquoise"    lightcolour 6 [lightrgb = "192 255 255"]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

mapmodellist = [
"tree1"
"dcp/tree2"
"dcp/tree3"
"dcp/palmtree"
"dcp/thorns"
"dcp/plant1"
"dcp/grass"
"dcp/ivy"
"dcp/barrel"
"dcp/streetlamp"
"dcp/bench"
"dcp/pillar"
"dcp/waterbowl"
"dcp/jumppad2"
"dcp/leafs"
"dcp/valve"
"dcp/palette"
"dcp/vent"
"dcp/mushroom"
"dcp/biotank"
"dcp/groundlamp"
"dcp/console"
"dcp/turret"
"carrot"
"switch1"
"switch2"
"doors/door_01"
"doors/door_02"
"doors/door_03"
"dcp/cask"
"dcp/cart"
"dcp/candle"
"dcp/vase"
"dcp/sack"
"dcp/chandelier"
"dcp/chest"
"dcp/firebowl"
"dcp/smplant"
"dcp/insect"
"dcp/reed"
"makke/nocamp"
"makke/strahler"
"tentus/food-drink/goblet"
"tentus/food-drink/apple"
"tentus/food-drink/pear"
"tentus/food-drink/appleslice"
"tentus/food-drink/meat"
"tentus/food-drink/bowl"
"tentus/food-drink/pieslice"
"tentus/food-drink/mug"
"tentus/food-drink/winebottle"
"tentus/food-drink/pie"
"tentus/books/flat"
"tentus/books/multi"
"tentus/chains/chain"
"tentus/chains/curvechain"
"tentus/barrel"
"tentus/sidebarrel"
"tentus/pot1"
"tentus/rope"
"tentus/ropelamp"
"tentus/ladder"
"tentus/fattree"
"tentus/moneybag"
"tentus/woodbench"
"tentus/hammer"
"tentus/anvil"
"tentus/spear"
"tentus/key"
"tentus/redshield"
"tentus/greenshield"
"tentus/bombs"
"objects/window01"
"objects/sign01"
"objects/lamp01"
"objects/chair01"
"objects/bed01"
"vegetation/tree00"
"vegetation/tree01"
"vegetation/bush01"
"vegetation/tree02"
"vegetation/tree04"
"vegetation/tree05"
"vegetation/tree06"
"vegetation/tree07"
"vegetation/tree03"
"objects/window02/window02_1"
"objects/window02/window02_2"
"objects/bench01"
"objects/lantern01"
"objects/lantern02"
"objects/woodchop"
"objects/table01"
"objects/torch"
"objects/torch_cold"
"objects/fire"
"objects/sail01"
"objects/well_base"
"objects/well_roof"
"objects/lamp02"
"objects/med_chand"]

newgui mapmodels [
    loop i (listlen $mapmodellist) [
        cmd = (concat newent mapmodel $i)
        guibutton (at $mapmodellist $i) $cmd
    ]
]

newgui monsters [
    guibutton "ogro / fireball"      "newent monster 0"
    guibutton "rhino / chaingun"      "newent monster 1"
    guibutton "ratamahatta / shotgun"      "newent monster 2"
    guibutton "slith / rifle" "newent monster 3"
    guibutton "bauul / RL"     "newent monster 4"
    guibutton "hellpig / bite"       "newent monster 5"
    guibutton "knight / iceball" "newent monster 6"
    guibutton "goblin / slimeball"   "newent monster 7"
]

newgui options [
    guicheckbox "2D menus" gui2d
    guicheckbox "show menu on start" startmenu
    guicheckbox "invert mouse"      invmouse
    guitext "mouse sensitivity (default: 3)"
    guislider sensitivity 1 (min (* 100 $sensitivityscale) 10000)
    guicheckbox "damage compass"    damagecompass
	guilist [
    	guicheckbox "hudguns"       hudgun
		if $hudgun [
			guibar
			guicheckbox "sway"		hudgunsway
		]
	]
    guicheckbox "blood"             blood
    guilist [
        guicheckbox "fullbright player models" fullbrightmodels 200 0
        if $fullbrightmodels [
            guibar
            guiradio "bright" fullbrightmodels 100
            guibar
            guiradio "overbright" fullbrightmodels 200
        ]
    ]
    guicheckbox "ogro"              ogro
    guicheckbox "always use team skins" teamskins
    guicheckbox "outline capture meters" outlinemeters
    guicheckbox "particle text"     particletext
    guilist [
        guicheckbox "crosshair color effects" crosshairfx
        if $crosshairfx [
            guibar
            guicheckbox "teammates" teamcrosshair
            guibar
            guicheckbox "hits" hitcrosshair 425
        ]
    ]

    guibutton "Restore Defaults" "exec data/defaults.cfg"

    guitab    "gfx"
    guitext "performance key: 0fast~, 2moderate~, 3slow and pretty~"
    guibar
    guilist [
        guicheckbox "2shaders	  "           shaders
	    if $shaders [
            guibar
            guiradio "0low detail" shaderdetail 1
            guibar
            guiradio "2high detail" shaderdetail 3
	    ]
    ]
    guilist [
        guitext "water	  "
        guibar
        guicheckbox "2refraction"  waterrefract
        guibar
        guicheckbox "3reflection"  waterreflect
        guibar
        guicheckbox "0caustics"    caustics
		if (&& (= $renderpath 0) $waterrefract) [
			guibar
        	guicheckbox "0depth fog"   refractfog      
		]
    ]
	if (> $renderpath 0) [
		guilist [
			guitext "waterfalls	  "
			guibar
			guicheckbox "2refraction" waterfallrefract
			guibar
			guicheckbox "0reflection" waterfallenv
		]
	]
    if (= $renderpath 0) [
        guicheckbox "0stencil shadows"   dynshadow 60 0
    ] [
		guilist [
        	guicheckbox "3soft shadows"  shadowmap
			if $shadowmap [
                guibar
        	    guiradio "2medium quality" shadowmapsize 9 [blurshadowmap 1]
    	        guibar
	            guiradio "3high quality" shadowmapsize 10 [blurshadowmap 2]
			] [
				guibar
				guicheckbox "0stencil shadows" dynshadow 60 0
			]
    	]
    	if $glare [
        	glarepreset = 0
        	if (= $glarescale 1) [
            	if (= $blurglare 4) [glarepreset = 1]
            	if (= $blurglare 7) [glarepreset = 3]
        	]
        	if (= $glarescale 2) [
            	if (= $blurglare 3) [glarepreset = 2]
            	if (= $blurglare 7) [glarepreset = 4]
        	]
        	guilist [
            	guicheckbox "3glare	  "       glare
            	guibar
            	guiradio "2subtle" glarepreset 1 [blurglare 4; glarescale 1]
            	guibar
            	guiradio "2glowy" glarepreset 2 [blurglare 3; glarescale 2]
            	guibar
            	guiradio "3soft" glarepreset 3 [blurglare 7; glarescale 1]
            	guibar
            	guiradio "3intense" glarepreset 4 [blurglare 7; glarescale 2]
        	]
    	] [
        	guicheckbox "3glare"         glare
    	]
    ]
    guicheckbox "2grass"             grass
    if (> $renderpath 0) [
        guicheckbox "0dynamic lights"    maxdynlights 3 0
        guicheckbox "0soft explosions" depthfx
	]
    guicheckbox "0water/lava vertex animation" vertwater
    guicheckbox "0glass reflection"  glassenv
    guicheckbox "0decals" decals
    guicheckbox "0fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures    "
        guibar
        guiradio "0low quality" maxtexsize 256
        guibar
        guiradio "2high quality" maxtexsize 0
    ]
    guilist [
        guitext "models	  "
        guibar
        guicheckbox "0lighting"    lightmodels
        guibar
        guicheckbox "0reflection"  envmapmodels
        guibar
        guicheckbox "0glow"        glowmodels
        if (> $renderpath 0) [
            guibar
            guicheckbox "0bumpmap" bumpmodels
        ]
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "fullscreen anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 1)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "bilinear filtering" bilinear
		guibar
		guicheckbox "trilinear filtering (mipmaps)" trilinear
	]

    guitab   "res"
    guilist [
        guilist [
            guibutton "320x240"              "screenres 320 240"
            guibutton "640x480"              "screenres 640 480"
            guibutton "800x600"              "screenres 800 600"
            guibutton "1024x768"             "screenres 1024 768"
            guibutton "1152x768"             "screenres 1152 768"
            guibutton "1200x960"             "screenres 1200 960"
        ]
		guibar
        guilist [
            guibutton "1280x800"             "screenres 1280 800"
            guibutton "1280x1024"            "screenres 1280 1024"
            guibutton "1440x900"             "screenres 1440 900"
            guibutton "1600x1200"            "screenres 1600 1200"
            guibutton "1680x1050"            "screenres 1680 1050"
            guibutton "1920x1200"            "screenres 1920 1200"
        ]
    ]

    guitab   "sound"
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen

    guitab    "postfx"
    guibutton "(effect OFF)"          "setfullscreenshader"
    guibutton "bloom (subtle: 80)"    "setfullscreenshader bloom 80"
    guibutton "bloom (bright: 140)"    "setfullscreenshader bloom 140"
    guibutton "bloom (intense: 200)"  "setfullscreenshader bloom 200"
    guibutton "sobel"  "setfullscreenshader sobel"
    guibutton "invert" "setfullscreenshader invert"
    guibutton "gbr"    "setfullscreenshader gbr"
    guibutton "bw"     "setfullscreenshader bw"

    guitab  "keybinds"
    guitext "(basic keybinds, for anything"
    guitext "more use the 'bind' command)"
    guitext "select action to bind:"
    guilistsplit n 2 $actions [
      guibutton $n (concat menubind $n)
    ]
]

actions = [forward backward left right jump attack saycommand sayteamcommand "universaldelta 1" "universaldelta -1" togglezoom edittoggle "setweapon 1" "setweapon 2" "setweapon 3" "setweapon 4"]


menubind = [ tobind = $arg1; showgui binditto ]
bindit = [ bind $arg1 $tobind ]

keys = "MOUSE1 MOUSE2 MOUSE3 MOUSE4 MOUSE5 SPACE LCTRL RCTRL ALT TAB ESCAPE BACKQUOTE SHIFT BACKSPACE ENTER INSERT DELETE HOME END PAGEUP PAGEDOWN A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 1 2 3 4 5 6 7 8 9 0 F1 F2 F3 F4 F5 F6 F7 F8 F9 F10 F11 F12"

newgui binditto [
    guilistsplit n 4 $keys [
      guibutton $n (concat bindit $n) 
    ]
]


entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]
    
genentattributes = [
    entattributes = ""
    n = ( listlen $arg2 )
    loop i $n [       
        do [
          t@i = [ guitext @(at $arg2 $i) ]
          a@i = [ guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate ]
          entattributes = [ @@entattributes @t@@i; @a@@i; ]
        ]
    ]
]

guilistsplit = [
  guilist [
    i = 0
    n = (listlen $arg3)
    z = (div (+ $n (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $n)
        while [< $i $t] [
          push $arg1 (at $arg3 $i)
          @@@arg4
          pop $arg1
          i = (+ $i 1)
        ]
      ]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton quicklight          "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
  guibutton "air"                        "editmat air"
  guibutton "water"                      "editmat water"
  guibutton "clip"                       "editmat clip"
  guibutton "glass"                      "editmat glass"
  guibutton "noclip"                     "editmat noclip"
  guibutton "lava"                       "editmat lava"
  guibutton "aiclip"                     "editmat aiclip"
  guibar
  guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "data/brush_1c.png" brush_0
  guiimage "data/brush_21c.png" brush_1
  guiimage "data/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "data/brush_3s.png" brush_3
  guiimage "data/brush_5s.png" brush_4
  guiimage "data/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "data/brush_3s.png" brush_6
  guiimage "data/brush_5s.png" brush_7
  guiimage "data/brush_7s.png" brush_8
  ]
]

newentgui = [
  do [
    genentattributes [@@arg1] [@@arg2] [@@arg3]
    newgui @arg1 [
      guitext $tmpt
      guibar
      @entattributes
      guitab type 
      guilistsplit n 2 $enttypelist [
          guibutton $n [ entset @n ]       
      ]  
      guitab misc
      @quickeditmenu
    ]
  ]
]

loop i $enttypelength [
  newentgui (at $enttypelist $i) "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui brushes [
  @brushmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model type trigger" "0 360 0 100 0 29 0 20"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui carrot "tag type" "0 50 0 29"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [ 
  if ($hmapedit) [
    showgui brushes
  ] [
    if (enthavesel) [
      initentgui; 
      do [ showgui (et) ] 
    ] [
      if (havesel) [
        showgui materials
      ] [
        showgui quickedit
      ]
    ]
  ]
]

showentgui = [ do [ contexteditgui ] ] // legacy bind





